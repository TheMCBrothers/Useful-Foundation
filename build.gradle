plugins {
    id "idea"
    id "maven-publish"
    id "net.minecraftforge.gradle" version "6.0.+"
    id "me.hypherionmc.cursegradle" version "2.0.1"
    id 'com.modrinth.minotaur' version "2.+"
}

group = "net.themcbrothers" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "usefulfoundation"
version = "$minecraft_version-$mod_version"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            mods {
                usefulfoundation {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            mods {
                usefulfoundation {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            args "--mod", "usefulfoundation", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            mods {
                usefulfoundation {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

processResources {
    filesMatching('META-INF/mods.toml') {
        expand 'file': ['jarVersion': version],
                'loader_version_range': loader_version_range,
                'forge_version_range': forge_version_range,
                'minecraft_version_range': minecraft_version_range,
                'themcbroslib_version_range': themcbroslib_version_range
    }
}

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Blamejared"
        url = "https://nexus.themcbrothers.net/repository/maven-blamejared"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://nexus.themcbrothers.net/repository/maven-mod"
    }
    maven {
        name = "themcb-nexus"
        url = "https://nexus.themcbrothers.net/repository/maven-releases/"
    }
    maven {
        name = "themcb-nexus-snapshots"
        url = "https://nexus.themcbrothers.net/repository/maven-snapshots/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

    if (project.hasProperty("jei_version")) {
        // JEI
        runtimeOnly fg.deobf("mezz.jei:jei-$minecraft_version-forge:$jei_version")
        compileOnly fg.deobf("mezz.jei:jei-$minecraft_version-common-api:$jei_version")
        compileOnly fg.deobf("mezz.jei:jei-$minecraft_version-forge-api:$jei_version")
    }

    // TheMCBrosLib
    implementation fg.deobf("net.themcbrothers:TheMCBrosLib:$themcbroslib_version")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Useful Foundation",
                "Specification-Vendor"    : "TheMCBrothers",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : jar.archiveVersion,
                "Implementation-Vendor"   : "TheMCBrothers",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        PublicationContainer publicationContainer ->
            publicationContainer.register("maven", MavenPublication) {
                MavenPublication publication ->
                    publication.artifacts = [jar]
                    publication.version = project.version
                    publication.artifactId = "useful-foundation"
            }
    }

    repositories {
        maven {
            credentials {
                username = getSecret("nexus_uname")
                password = getSecret("nexus_pwd")
            }

            url "https://nexus.themcbrothers.net/repository/maven-releases/"
        }
    }
}

private String getSecret(String key) {
    def secret = System.getenv(key)
    if (secret != null) {
        return secret.trim()
    }

    File secretFile = file('secrets.properties')
    if (secretFile.canRead()) {
        Properties secretProps = new Properties()
        secretProps.load(new FileInputStream(secretFile))
        return secretProps.getProperty(key).toString()
    }

    return null
}

var curseForgeKey = getSecret('cf_key')
var modrinthToken = getSecret('modrinth_token')
var refName = System.getenv('ref_name') ?: 'HEAD'

// Publish to CurseForge
if (curseForgeKey != null) {
    curseforge {
        apiKey = curseForgeKey

        project {
            id = curse_project
            changelog = file('changelog.txt')
            changelogType = "markdown"
            releaseType = artifact_type
            addGameVersion(minecraft_version)
            mainArtifact(tasks.jar) {
                displayName = "$mod_name $mod_version"
                relations {
                    requiredDependency "themcbroslib"
                }
            }
        }
    }
}

// Publish to Modrinth
if (modrinthToken != null) {
    modrinth {
        token = modrinthToken
        projectId = modrinth_project
        versionName = "$mod_name $mod_version"
        versionType = artifact_type
        changelog = "[changelog.txt](https://raw.githubusercontent.com/TheMCBrothers/Useful-Foundation/$refName/changelog.txt)"
        uploadFile = tasks.jar
        dependencies {
            required.project "themcbroslib"
        }
    }
}
